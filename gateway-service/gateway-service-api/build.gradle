buildscript {
    ext.projectGroup = 'ru.udya.services'
    ext.projectVersion = '1.0.0-SNAPSHOT'

    ext.commonRepositories = {
        mavenLocal()
        mavenCentral()
    }

    repositories commonRepositories

    dependencies {
        classpath group: 'org.openapitools', name: 'openapi-generator-gradle-plugin', version: '5.4.0'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version:'2.6.4'
        classpath group: 'io.spring.gradle', name:'dependency-management-plugin', version: '1.0.11.RELEASE'

    }
}

def modulePrefix = ':gateway-service:gateway-service-api'

def webclientModule = project("${modulePrefix}:gateway-service-api-webclient-client")

def portalApiSpec = "${projectDir}/config/gateway-service-api.yaml"

ext {
    set('projectGroup', 'ru.hse')
    set('projectVersion', '1.4.0-SNAPSHOT')

    set('springCloudVersion', "2021.0.1")
}

version = projectVersion

configure(webclientModule) {
    apply(plugin: 'java')
    apply(plugin: 'org.openapi.generator')

    repositories commonRepositories

    group = projectGroup
    version = projectVersion

    configurations {
        swaggerCodegen {
            description = 'Swagger codegen sdk'
        }
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    artifacts {
        archives sourcesJar
    }
}

configure(webclientModule) {

    apply(plugin: 'org.springframework.boot')
    apply(plugin: 'io.spring.dependency-management')

    def portalApiGeneratedSrcDir = "$buildDir/gateway-service-api-webclient-config"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java",
                           "${portalApiGeneratedSrcDir}/src/main/java"]
            }
        }
    }

    def swagger_annotations_version = "1.6.3"
    def jackson_version = "2.11.4"
    def jackson_databind_version = "2.11.4"
    def jackson_databind_nullable_version = "0.2.2"
    def jakarta_annotation_version = "1.3.5"
    def junit_version = "4.13.1"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
        implementation "com.google.code.findbugs:jsr305:3.0.2"
        implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
        implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
        implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
        implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"

        testImplementation "junit:junit:$junit_version"
    }

    task generateJavaClientSources(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "java"
        inputSpec = portalApiSpec
        outputDir = portalApiGeneratedSrcDir
        configFile = "${projectDir}/config/gateway-service-api-webclient-config.json"
        validateSpec = true
//        verbose = true
    }

    compileJava.dependsOn generateJavaClientSources

    task distribution(dependsOn: jar)
}
buildscript {
    ext.projectGroup = 'ru.udya.services'
    ext.projectVersion = '1.0.0-SNAPSHOT'

    ext.commonRepositories = {
        mavenLocal()
        mavenCentral()
    }

    repositories commonRepositories

    dependencies {
        classpath group: 'org.openapitools', name: 'openapi-generator-gradle-plugin', version: '5.4.0'
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version:'2.6.4'
        classpath group: 'io.spring.gradle', name:'dependency-management-plugin', version: '1.0.11.RELEASE'

    }
}

def modulePrefix = ':employee-service:employee-service-api'

def serverModule = project("${modulePrefix}:employee-service-api-server")
def okHttpModule = project("${modulePrefix}:employee-service-api-okhttp-client")
def feignModule = project("${modulePrefix}:employee-service-api-feign-client")
def webclientModule = project("${modulePrefix}:employee-service-api-webclient-client")

def portalApiSpec = "${projectDir}/config/employee-service-api.yaml"

ext {
    set('projectGroup', 'ru.hse')
    set('projectVersion', '1.4.0-SNAPSHOT')

    set('springCloudVersion', "2021.0.1")
}

version = projectVersion

configure([serverModule, okHttpModule, feignModule, webclientModule]) {
    apply(plugin: 'java')
    apply(plugin: 'org.openapi.generator')

    repositories commonRepositories

    group = projectGroup
    version = projectVersion

    configurations {
        swaggerCodegen {
            description = 'Swagger codegen sdk'
        }
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    artifacts {
        archives sourcesJar
    }
}

configure(serverModule) {

    apply(plugin: 'org.springframework.boot')
    apply(plugin: 'io.spring.dependency-management')

    def portalApiGeneratedSrcDir = "$buildDir/employee-service-api-server"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java",
                           "${portalApiGeneratedSrcDir}/src/main/java"]
            }
        }
    }

    // copied from generated pom.xml
    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web"
        testImplementation "org.springframework.boot:spring-boot-starter-validation"
        implementation "org.springframework.data:spring-data-commons"

        implementation "org.springdoc:springdoc-openapi-ui:1.6.4"
        implementation "com.google.code.findbugs:jsr305:3.0.2"
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        implementation "org.openapitools:jackson-databind-nullable:0.2.2"
        implementation "com.fasterxml.jackson.core:jackson-databind"
    }


    task generateJavaServerSources(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "spring"
        inputSpec = portalApiSpec
        outputDir = portalApiGeneratedSrcDir
        configFile = "${projectDir}/config/employee-service-api-server-config.json"
        validateSpec = true
//        verbose = true
    }

    compileJava.dependsOn generateJavaServerSources

    task distribution(dependsOn: jar)
}

configure(feignModule) {

    apply(plugin: 'org.springframework.boot')
    apply(plugin: 'io.spring.dependency-management')

    def portalApiGeneratedSrcDir = "$buildDir/employee-service-api-feign-client"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java",
                           "${portalApiGeneratedSrcDir}/src/main/java"]
            }
        }
    }

// copied from generated pom.xml
    dependencies {
        implementation "org.springframework.data:spring-data-commons"

        implementation "org.springframework.boot:spring-boot-starter-validation"
        testImplementation "org.springframework.boot:spring-boot-starter-test"

        implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

        implementation "org.springdoc:springdoc-openapi-ui:1.6.4"
        implementation "com.google.code.findbugs:jsr305:3.0.2"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        implementation "org.openapitools:jackson-databind-nullable:0.2.2"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    task generateJavaClientSources(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "spring"
        inputSpec = portalApiSpec
        outputDir = portalApiGeneratedSrcDir
        configFile = "${projectDir}/config/employee-service-api-feign-config.json"
        validateSpec = true
//        verbose = true
    }

    compileJava.dependsOn generateJavaClientSources

    task distribution(dependsOn: jar)
}

configure(okHttpModule) {

    def portalApiGeneratedSrcDir = "$buildDir/employee-service-api-okhttp-client"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java",
                           "${portalApiGeneratedSrcDir}/src/main/java"]
            }
        }
    }

    dependencies {
        implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
        implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
        implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
        implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.0'
        implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.4.0'
        implementation group: 'io.gsonfire', name: 'gson-fire', version: '1.7.1'
        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }

    task generateJavaClientSources(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "java"
        inputSpec = portalApiSpec
        outputDir = portalApiGeneratedSrcDir
        configFile = "${projectDir}/config/employee-service-api-okhttp-config.json"
        validateSpec = true
//        verbose = true
    }

    compileJava.dependsOn generateJavaClientSources

    task distribution(dependsOn: jar)
}

configure(webclientModule) {

    apply(plugin: 'org.springframework.boot')
    apply(plugin: 'io.spring.dependency-management')

    def portalApiGeneratedSrcDir = "$buildDir/employee-service-api-webclient-config"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java",
                           "${portalApiGeneratedSrcDir}/src/main/java"]
            }
        }
    }

    def swagger_annotations_version = "1.6.3"
    def jackson_version = "2.11.4"
    def jackson_databind_version = "2.11.4"
    def jackson_databind_nullable_version = "0.2.2"
    def jakarta_annotation_version = "1.3.5"
    def junit_version = "4.13.1"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
        implementation "com.google.code.findbugs:jsr305:3.0.2"
        implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
        implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
        implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
        implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"

        testImplementation "junit:junit:$junit_version"
    }

    task generateJavaClientSources(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "java"
        inputSpec = portalApiSpec
        outputDir = portalApiGeneratedSrcDir
        configFile = "${projectDir}/config/employee-service-api-webclient-config.json"
        validateSpec = true
//        verbose = true
    }

    compileJava.dependsOn generateJavaClientSources

    task distribution(dependsOn: jar)
}